using panaedra.msroot.msutil.logic.*.
using panaedra.msroot.msutil.interfaces.*.

{panaedra/msroot/msutil/logic/top_i/ctop_class.i} 

class panaedra.msroot.msutil.logic.sc_string_substitute: 

  /****************************** Source Header ********************************\

      Description: String substitution, for example os-environment vars inside strings

  \*****************************  Include Files ********************************/

  /******************************* $NoKeywords:  $ *****************************/


  method public static character ExpandOsEnv
    
    /* See other overload. */
       
    (cInputIP# as character):
    
    return ExpandOsEnv(cInputIP#, false).
    
  end method. /* ExpandOsEnv , with 'strip unset' to false */


  method public static character ExpandOsEnv
    
    /* This method first tries explicit unix syntax. 
       Example: "sometext$往轴蟒犷浃往渝泔钿轴蟒祜蝓黹痼蹴涉躅轼簌铘狲轶铒骘躅洮ア鏖钿秣篝犷溽蜾狎趄殄洚涉庥趄轲疹箦粝笈铞尚轶驷祗瀣ネ轴颔鏖祆蝈趱蝾ネ轴颔殒翳孱鲩蝻铐孱鲠蜷徕戾轶铒箦舢涉庥趄轲疹箦粝笈铞尚轶趄蹂ネ轴颔鏖祆蝈趱蝾殒翳孱鲩蝻铐孱鲠蜷徕戾轶铒箦舢ㄣ深瘐羯校狍汨狎徙翦颥庥趄轲疹箦粝笈铞尚狍祜玳汜飑溴骈铄鲠蜷徕戾樯钿屮狍轭翦珏铒躅滹溴骈铄鲠蜷徕戾樯钿屮郁狎簦狍轭翦珏铒躅滹溴骈铄鲠蜷徕戾樯钿屮蓬洌狍轭翦珏铒躅滹溴骈铄鲠蜷徕戾榕钿狍轭翦珏铒躅滹溴骈铄鲠蜷徕戾阆笈铞狍汨狎徙翦铒躅滹樯钿屮轭溴ㄣ深瘐羯校ГЗ殒樯钿屮翳孱滹镱弪蝻躅滹翳蝻骱膺麽祀滹祆狎滹麒殪樯钿屮昂狍箝珙榕钿轭溴ㄣ深瘐羯校', iIndex#).
          
        if ((iIndex# + 2) <= 0) or ((iEnd# - iIndex# - 2) <= 0) then leave b_walkdollar. /* This can happen if string ends with (literal) dollar sign */
          
        assign
          cOsEnv# = os-getenv(substring(cInputIP#, iIndex# + 2, iEnd# - iIndex# - 2)).
        
        if cOsEnv# = ? then do:
          if bStripUnsetOsEnvIP# then cOsEnv# = "". 
          else cOsEnv# = substring(cInputIP#, iIndex# + 2, iEnd# - iIndex# - 2).
        end.
        
        if (iIndex# <= 0 ) or ((iEnd# - iIndex# + 1) <= 0) then leave b_walkdollar.  /* This can happen if string ends with (literal) dollar sign */
        
        substring(cInputIP#, iIndex#, iEnd# - iIndex# + 1) = cOsEnv#.
      
        iIndex# = index(cInputIP#,'$').
       
      end.
      
      catch oError# as Progress.Lang.Error:
        /* Nothing to do. Extra safety, this happened once during an exceptional error flow, hiding original error. */
      end catch.
     
    end. /* dollar sign found */
    else 
    do:
      
      iIndexStart# = index(cInputIP#,'%').
      
      if iIndexStart# > 0 then
      b_search_dollar: 
      do on error undo, throw:
        
        do while iIndexStart# > 0:
          
          iIndexEnd# = index(cInputIP#,'%',iIndexStart# + 1).
          
          if iIndexEnd# = 0 then leave b_search_dollar.
          
          else 
          do:
            
            cOsEnv# = os-getenv(substring(cInputIP#, iIndexStart# + 1, iIndexEnd# - iIndexStart# - 1)).
            
            if sc_data:HasValue(cOsEnv#) or bStripUnsetOsEnvIP# then do: 
              if not sc_data:HasValue(cOsEnv#) then cOsEnv# = "".
              substring(cInputIP#, iIndexStart#, iIndexEnd# - iIndexStart# + 1) = cOsEnv#.
            end.
           
            assign
              iIndexStart# = index(cInputIP#,'%',iIndexEnd# + 1).
            iIndexEnd#   = 0.
          end.
          
        end.
        
        catch oError# as Progress.Lang.Error:
          /* Nothing to do. Extra safety, this happened once during an exceptional error flow, hiding original error. */
        end catch.
        
      end.
      
    end.
    
    return cInputIP#.
  
  end method. /* ExpandOsEnv , with bStripUnsetOsEnvIP# */

end class.

/* EOF */
